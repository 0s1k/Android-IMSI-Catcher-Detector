apply plugin: 'com.android.application'

def gitSha = 'na'
try {
    gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
} catch (Exception e) {
    logger.warn("git not available")
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 19 // Do not change: Working Icons on Android 5+
        versionCode 36
        versionName '0.1.36-alpha'
        testApplicationId "com.SecUpwN.AIMSICD.test"
    }

    buildTypes {
        debug {
            try {
                buildConfigField 'String', 'OPEN_CELLID_API_KEY', '\"' + open_cellid_api_key + '\"'
            } catch (MissingPropertyException e) {
                buildConfigField 'String', 'OPEN_CELLID_API_KEY', '"NA"'
            }
            try {
                buildConfigField 'String', 'BUILDOZER_BUILDNUMBER', '\"' + System.getenv("BUILDOZER_BUILDNUMBER") + '\"'
            } catch (MissingPropertyException e) {
                buildConfigField 'String', 'BUILDOZER_BUILDNUMBER', '"NA"'
            }
            if (!gitSha.equals('na')) {
                versionNameSuffix '-' + gitSha
            } else {
                versionNameSuffix '-debug'
            }
        }
        release {
            buildConfigField 'String', 'OPEN_CELLID_API_KEY', '"NA"'
            buildConfigField 'String', 'BUILDOZER_BUILDNUMBER', '"NA"'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            versionNameSuffix '-release'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }


    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    // DO NOT REMOVE BELOW COMMENTED-OUT CODE BEFORE ASKING!
    //compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    compile 'com.android.support:appcompat-v7:22.2.1'
    //https://github.com/lp0/slf4j-android
    compile fileTree(dir: 'libs', include: '*.jar')
    compile project(':third_party:rootshell')
    compile 'org.osmdroid:osmdroid-android:4.2'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'eu.lp0.slf4j:slf4j-android:1.7.12-0'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'au.com.bytecode:opencsv:2.4'
    compile files('libs/osmbonuspack_v5.1.jar')
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.github.kaichunlin.transition:core:0.9.2'
    compile 'io.freefair.android-util:logging:1.1.0'
    //debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    //releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
